# Copyright 2017 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//services/service_manager/public/cpp/service.gni")
import("//services/service_manager/public/service_manifest.gni")
import("//testing/test.gni")

source_set("lib") {
  sources = [
    "common.cc",
    "common.h",
    "ml_service.cc",
    "ml_service.h",
  ]

  if (is_linux) {
    sources += [
      "compilation_impl_linux.cc",
      "compilation_impl_linux.h",
      "execution_impl_linux.cc",
      "execution_impl_linux.h",
      "model_impl_linux.cc",
      "model_impl_linux.h",
      "neural_network_impl_linux.cc",
      "neural_network_impl_linux.h",
    ]
  } else if (is_android) {
    sources += [
      "compilation_impl_android.cc",
      "compilation_impl_android.h",
      "execution_impl_android.cc",
      "execution_impl_android.h",
      "model_impl_android.cc",
      "model_impl_android.h",
      "neural_network_impl_android.cc",
      "neural_network_impl_android.h",
    ]

    include_dirs = [
      "${android_ndk_root}/sysroot/usr/include"
    ]

    libdir = ""
    if (current_cpu == "x86") {
      libdir = "arch-x86/usr/lib"
    } else if (target_cpu == "arm") {
      libdir = "arch-arm/usr/lib"
    } else if (target_cpu == "mips") {
      libdir = "arch-mips/usr/lib"
    } else if (target_cpu == "arm64") {
      libdir = "arch-arm64/usr/lib"
    } else if (target_cpu == "x64") {
      libdir = "arch-x86_64/usr/lib64"
    } else if (target_cpu == "mips64") {
      libdir = "arch-mips64/usr/lib64"
    } else {
      assert(false, "Need android neuralnetworks support for your target arch.")
    }
    lib_dirs = [
      "${android_ndk_root}/platforms/android-${android_sdk_platform_version}/${libdir}"
    ]

    libs = [
      "neuralnetworks",
    ]

  } else if (is_mac) {
    sources += [
      "compilation_impl_mac.mm",
      "compilation_impl_mac.h",
      "execution_impl_mac.mm",
      "execution_impl_mac.h",
      "model_impl_mac.mm",
      "model_impl_mac.h",
      "neural_network_impl_mac.mm",
      "neural_network_impl_mac.h",
      "mpscnn_context.mm",
      "mpscnn_context.h", 
    ]

    libs = [
      "Metal.framework",
      "MetalPerformanceShaders.framework",
      "Accelerate.framework",
    ]
  } else {
    sources += [
      "neural_network_impl.cc",
      "neural_network_impl.h",
    ]
  }

  deps = [
    "//mojo/public/cpp/bindings",
  ]

  public_deps = [
    "//base",
    "//services/service_manager/public/cpp",
    "//services/ml/public/interfaces",
  ]
}

service_manifest("manifest") {
  name = "ml"
  source = "manifest.json"
}

